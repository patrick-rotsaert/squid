#
# Copyright (C) 2022-2023 Patrick Rotsaert
# Distributed under the Boost Software License, Version 1.0.
# (See accompanying file LICENSE or copy at
# http://www.boost.org/LICENSE_1_0.txt)
#

cmake_minimum_required(VERSION 3.19)

include(CMakeFindDependencyMacro)

foreach(dependency @PROJECT_DEPENDENCIES@)
	find_dependency(${dependency} REQUIRED)
endforeach()

set(@PROJECT_NAME@_known_comps static shared)
set(@PROJECT_NAME@_comp_static NO)
set(@PROJECT_NAME@_comp_shared NO)

foreach(comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
	if(comp IN_LIST @PROJECT_NAME@_known_comps)
		set(@PROJECT_NAME@_comp_${comp} YES)
	else()
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "@PROJECT_NAME@ does not have a component '${comp}'.")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
		return()
	endif()
endforeach()

if(@PROJECT_NAME@_comp_static AND @PROJECT_NAME@_comp_shared)
	set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "@PROJECT_NAME@ 'static' and 'shared' components are mutually exclusive.")
	set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
	return()
endif()

set(@PROJECT_NAME@_static_targets "${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-static-targets.cmake")
set(@PROJECT_NAME@_shared_targets "${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-shared-targets.cmake")

macro(@PROJECT_NAME@_load_targets type)
	if(NOT EXISTS "${@PROJECT_NAME@_${type}_targets}")
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "@PROJECT_NAME@ '${type}' libraries were requested but not found.")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
		return()
	endif()
	include("${@PROJECT_NAME@_${type}_targets}")
endmacro()

if(@PROJECT_NAME@_comp_static)
	@PROJECT_NAME@_load_targets(static)
elseif(@PROJECT_NAME@_comp_shared)
	@PROJECT_NAME@_load_targets(shared)
elseif(DEFINED @PROJECT_NAME@_SHARED_LIBS AND @PROJECT_NAME@_SHARED_LIBS)
	@PROJECT_NAME@_load_targets(shared)
elseif(DEFINED @PROJECT_NAME@_SHARED_LIBS AND NOT @PROJECT_NAME@_SHARED_LIBS)
	@PROJECT_NAME@_load_targets(static)
elseif(BUILD_SHARED_LIBS)
	if(EXISTS "${@PROJECT_NAME@_shared_targets}")
		@PROJECT_NAME@_load_targets(shared)
	else()
		@PROJECT_NAME@_load_targets(static)
	endif()
else()
	if(EXISTS "${@PROJECT_NAME@_static_targets}")
		@PROJECT_NAME@_load_targets(static)
	else()
		@PROJECT_NAME@_load_targets(shared)
	endif()
endif()
